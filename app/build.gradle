apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def keyStore = file('gank.jks')

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {

    compileSdkVersion 26

    defaultConfig {
        applicationId 'com.johnny.gank'
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 2
        versionName '1.1'
        flavorDimensions 'market'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        release {
            storeFile keyStore
            storePassword 'gank666'
            keyAlias 'Gank'
            keyPassword '94a186'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(keyStore.exists()) {
                println "using johnnyshieh's key"
                signingConfig signingConfigs.release
            }
            applicationVariants.all { variant ->
                if (variant.buildType.name == 'release') {
                    variant.outputs.all { output ->
                        def outputFile = output.outputFile
                        if (outputFile != null && outputFile.name.endsWith('.apk')) {
                            def fileName = "Gank_v${defaultConfig.versionName}_${releaseTime()}_${variant.flavorName}.apk"
                            output.outputFileName = new File(outputFile.parent, fileName)
                        }
                    }
                }
            }

            productFlavors.all { flavor ->
                manifestPlaceholders.put("UMENG_CHANNEL_VALUE",name)
            }
        }

        productFlavors {
            fir {
                dimension 'market'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$junit_version"
    implementation(name:'feedbacksdk', ext:'aar')

    // Support
    implementation "com.android.support:appcompat-v7:$appcompat_version"
    implementation "com.android.support:design:$design_version"
    implementation "com.android.support:recyclerview-v7:$recycler_view_version"
    implementation "com.android.support:cardview-v7:$card_view_version"

    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"

    // Dagger 2
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // RxJava 2
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // RxFlux
    implementation "com.johnny.rxflux:rxflux:$rxflux_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    implementation "com.umeng.analytics:analytics:latest.integration"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    // log
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation "com.github.tianzhijiexian:logger:$logger_version"
}
